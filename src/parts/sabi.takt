// 16 measures: C -> G -> Am -> Em -> F -> C -> F -> G

def sabi(ochisabi = false, lastsabi = false) {
  var tkey = 0
  if (lastsabi) {
    tkey = 1
  }

  var chord = {
    triad1 triad5 triad6 triad3
    triad4 triad1 triad4 triad5
  }

  [
    percussion: {
      for (i, 1, 16) {
        [
          {bas r}\ @ 4
          {{r r snr r}\ if (i == 16) {{snr @ 3 r}\\ @ 2} else {{r r snr r}\}}
          if (!ochisabi) { chh\ @ 8 }
        ]
      }
    }

    vocal: {
      c c c d e* e a
      g* e d e**
      a* a a g. a\\ g\\ e\ d c\
      c _a c d e**
      r** r*~~\ g\
      a*~~\ e\\ d\\ c**
    } | Transpose(tkey)

    piano_lh: {
      chord @ {[$1 $2 $3]. $2\ $3\ $2\ $1}
    } | Transpose(tkey)

    piano_rh: {
      chord @ {$1 $3 $1 $3}
    } | Transpose(tkey)

    bass: {
      for (i, 0, 1) {
        if (ochisabi && i == 0) {
          // ochi-sabi first half
          {^c g a e f ^c f g} @ {c**}
        } else {
          {^c g a e f ^c f g} | AddNotes(n+=12) @ {^{$1 $2} @ 4}\
        }
      }
    } | Transpose(tkey)
  ]
}
